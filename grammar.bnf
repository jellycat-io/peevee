<program> ::= <statements>

<statements> ::= { <statement> }

<statement> ::= <block-statement>
              | <expression-statement>
              | <variable-statement>
              | <if-statement>

<block-statement> ::= "INDENT" <statements> "DEDENT"

<expression-statement> ::= <expression>

<variable-statement> ::= "LET" <variable-declaration-list>

<variable-declaration-list> ::= <variable-declaration> { "," <variable-declaration> }

<variable-declaration> ::= <identifier> ["=" <variable-initializer>]

<variable-initializer> ::= <expression>

<if-statement> ::= "if" <expression> "then" <statement> ["else" <statement>]

<expression> ::= <assignment-expression>

<assignment-expression> ::= <additive-expression> [<assignment-operator> <assignment-expression>]

<additive-expression> ::= <multiplicative-expression> {("+" | "-") <multiplicative-expression>}

<multiplicative-expression> ::= <primary-expression> {("*" | "/" | "%") <primary-expression>}

<assignment-operator> ::= "="

<primary-expression> ::= <literal>
                       | <grouped-expression>
                       | <identifier>

<grouped-expression> ::= "(" <expression> ")"

<literal> ::= <integer-literal>
            | <float-literal>
            | <string-literal>

<integer-literal> ::= INT

<float-literal> ::= FLOAT

<string-literal> ::= STRING

<identifier> ::= IDENT
