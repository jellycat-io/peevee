<program> ::= <statements>

<statements> ::= <statement> <statements> | Îµ

<statement> ::= <block-statement>
              | <variable-statement>
              | <expression-statement>

<block-statement> ::= "INDENT" <statements> "DEDENT"

<variable-statement> ::= "LET" <variable-declaration-list>

<variable-declaration-list> ::= <variable-declaration> {"," <variable-declaration>}

<variable-declaration> ::= <identifier> ["=" <variable-initializer>]

<variable-initializer> ::= <assignment-expression>

<expression-statement> ::= <expression>

<expression> ::= <assignment-expression>

<assignment-expression> ::= <additive-expression> [<assignment-operator> <assignment-expression>]

<assignment-operator> ::= "="
                        | "+="
                        | "-="
                        | "*="
                        | "/="

<additive-expression> ::= <multiplicative-expression> {("+" | "-") <multiplicative-expression>}

<multiplicative-expression> ::= <primary-expression> {("*" | "/" | "%") <primary-expression>}

<primary-expression> ::= <literal>
                       | <identifier>
                       | <grouped-expression>

<grouped-expression> ::= "(" <expression> ")"

<literal> ::= <integer-literal>
            | <float-literal>
            | <string-literal>

<integer-literal> ::= INT

<float-literal> ::= FLOAT

<string-literal> ::= STRING

<identifier> ::= IDENT
